(new File(projectDir, "src/commonMain/kotlin/com/soywiz/korio/internal/KorioVersion.kt")).with {
	parentFile.mkdirs()
	String newText = "package com.soywiz.korio.internal\n\ninternal const val KORIO_VERSION = \"$projectVersion\""
	if (it.text != newText) it.text = newText
}

kotlin {
	sourceSets {
		nonJsMain
		nativePosixAppleMain
		nativePosixNonAppleMain
		nativeCommonMain
		nativeCommonTest
		nativePosixMain
		mingwX64Main {
			dependsOn nativeCommonMain
		}
		mingwX64Test {
			dependsOn nativeCommonTest
		}
		configure([iosX64Main, iosArm32Main, iosArm64Main, macosX64Main]) {
			dependsOn nativePosixAppleMain
			dependsOn nativeCommonMain
			dependsOn nativePosixMain
		}
		configure([linuxX64Main]) {
			dependsOn nativePosixNonAppleMain
			dependsOn nativeCommonMain
			dependsOn nativePosixMain
		}
		configure([iosX64Test, iosArm32Test, iosArm64Test, macosX64Test, linuxX64Test]) {
			dependsOn nativeCommonTest
			dependsOn nonJsMain
		}
		configure([jvmMain]) {
			dependsOn nonJsMain
		}
	}
}

dependencies {
	//jvmMainImplementation "org.java-websocket:Java-WebSocket:1.3.8"

	def coroutines = "kotlinx-coroutines-core"
	commonMainApi "org.jetbrains.kotlinx:$coroutines-common:$coroutinesVersion"
	jvmMainApi "org.jetbrains.kotlinx:$coroutines:$coroutinesVersion"
	jsMainApi "org.jetbrains.kotlinx:$coroutines-js:$coroutinesVersion"
	linuxX64MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_linux_x64:$coroutinesVersion"
	mingwX64MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_mingw_x64:$coroutinesVersion"
	macosX64MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_macos_x64:$coroutinesVersion"
	iosX64MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_ios_x64:$coroutinesVersion"
	iosArm32MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_ios_arm32:$coroutinesVersion"
	iosArm64MainApi "org.jetbrains.kotlinx:$coroutines-native_debug_ios_arm64:$coroutinesVersion"

	commonMainApi "com.soywiz:klock:$klockVersion"
	commonMainApi "com.soywiz:kds:$kdsVersion"
	commonMainApi "com.soywiz:kmem:$kmemVersion"
}
